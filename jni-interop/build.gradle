plugins {
    id 'kotlin-multiplatform'
    id 'maven-publish'
}

group 'jni'
version '1.0'

def localRepo = rootProject.file('build/.m2-repo')

publishing {
    repositories {
        maven { url = "file://$localRepo" }
    }
}

task cleanLocalRepo(type: Delete) {
    delete localRepo
}

// Determine host preset.
def hostPreset = MPPTools.defaultHostPreset(project, [kotlin.presets.macosX64, kotlin.presets.linuxX64, kotlin.presets.mingwX64])

kotlin {
    targets {
        fromPreset(hostPreset, 'jni') {
            compilations.main.cinterops {
                jni {
                    switch (hostPreset) {
                        case presets.macosX64:
                            includeDirs.headerFilterOnly "$System.env.JAVA_HOME/include", "$System.env.JAVA_HOME/include/darwin"
                            break
                        case presets.mingwX64:
                            includeDirs.headerFilterOnly "$System.env.JAVA_HOME/include", "$System.env.JAVA_HOME/include/win32"
                            break
                        case presets.linuxX64:
                            includeDirs.headerFilterOnly "$System.env.JAVA_HOME/include", "$System.env.JAVA_HOME/include/linux"
                            break
                    }
                    packageName 'jni'
                    extraOpts '-nodefaultlibs'
                }
            }

            mavenPublication {
                pom {
                    withXml {
                        def root = asNode()
                        root.appendNode('name', 'jni interop')
                        root.appendNode('description', 'A library providing interoperability with jni')
                    }
                }
            }
        }
    }
}
