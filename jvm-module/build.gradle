plugins {
    id 'kotlin-multiplatform'
    id 'maven-publish'
}

group 'alt.v.module.jvm'
version '1.0'

def localRepo = rootProject.file('build/.m2-repo')

publishing {
    repositories {
        maven { url = "file://$localRepo" }
    }
}

task cleanLocalRepo(type: Delete) {
    delete localRepo
}

// Determine host preset.
def hostPreset = MPPTools.defaultHostPreset(project, [kotlin.presets.macosX64, kotlin.presets.linuxX64, kotlin.presets.mingwX64])

kotlin {
    sourceSets {
        jvmModule {
            dependencies {
                implementation project(":jni-interop")
            }
        }
    }
    targets {
        fromPreset(hostPreset, 'jvmModule') {
            mavenPublication {
                pom {
                    withXml {
                        def root = asNode()
                        root.appendNode('name', 'jvm module')
                        root.appendNode('description', 'the jvm module')
                    }
                }
            }
        }
    }
}
